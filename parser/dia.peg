{
  function filterItems (first, rows) {
    var base = first ? [first] : []
    return base.concat(rows.map(function (row) {
      return row[1]
    }))
  }
  function sum (values) {
    return values.reduce(function (v, s) { return s + v }, 0)
  }
  function sumValues (items) {
    return sum(items.map(function (i) { return i.value }))
  }
}

start = _? i:block? b:(blank* block)* blank* !. { return filterItems(i, b) }
block
  = _? n:name colon newline c:compras a:additions? t:total? blank* {
      a = a ? a : [];
      var realTotal = sumValues(c) + sumValues(a);
      if (t && t != realTotal) a.push({value: (t - realTotal), desc: 'diferença'})
      
      return {
        kind: 'compra',
        fornecedor: n,
        items: c,
        extras: a,
        total: t || realTotal
      } 
    }
  / _? sc:saida_conta _? { sc.kind = 'saída/conta'; return sc; }
  / _? c:conta _? { c.kind = 'conta'; return c; }
  / _? s:saida _? { s.kind = 'saída'; return s; }
  / _? e:entrada _? { e.kind = 'entrada'; return e; }
  / _? s:saldo _? { s.kind = 'saldo'; return s; }
  / _? v:venda _? { v.kind = 'venda'; return v; }
  / _? a:any+ _? { return {kind: 'comment', note: a.join('')} }
  
saida_conta
  = 'sa'i [ií]i 'da'i _ 'para'i sep? _ c:conta { return c }
  / 'sa'i [ií]i 'da'i _ 'p/'i sep? _ c:conta { return c }
  / 'sa'i [ií]i 'da'i (_ name)? _ sep? c:conta { return c }
  / 'retirada'i _ 'para'i sep? _ c:conta { return c }
  / 'retirada'i _ 'p/'i sep? _ c:conta { return c }
  / 'retirada'i (_ name)? sep? _ c:conta { return c }
  
conta
  = 'pag'i ('amento'i/'o'i)? sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'conta'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'boleto'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'fatura'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'taxa'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  
saida
  = 'sa'i [ií]i 'da'i _ 'para'i sep? e:(name value_sep) p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'sa'i [ií]i 'da'i _ 'p/'i sep? e:(name value_sep) p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'sa'i [ií]i 'da'i (_ name)? sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'retirada'i _ 'para'i sep? e:(name value_sep) p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'retirada'i _ 'p/'i sep? e:(name value_sep) p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'retirada'i (_ name)? sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }

entrada
  = 'entrou'i _ 'do'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'entrou'i _ 'de'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'entrou'i (_ name)? sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'entraram'i (_? sep?) p:preco e:(_ name)? { return {value: p, desc: e ? e[1] : ''} }
  / 'entrada'i _ 'do'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'entrada'i _ 'de'i sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }
  / 'entrada'i _ 'de'i _ sep? p:preco e:(_ name)? { return {value: p, desc: e ? e[0] : ''} }
  / 'entrada'i (_ name)? sep? e:(name value_sep)? p:preco { return {value: p, desc: e ? e[0] : ''} }

saldo
  = 'saldo'i _ d:name value_sep? p:preco { return {value: p, desc: d} }
  / 'saldo'i value_sep p:preco { return {value: p} }

sep = _? ':' _? / _? '-' _? / _? '|' _? / _? ';' _? / _ 'de' _

compras = f:item r:(newline item)* { return filterItems(f, r) }
additions = newline _* f:addition r:(newline addition)* { return filterItems(f, r) }
addition = _? addition:_addition { return addition }
_addition
  = '+' _? v:preco { return {value: v} }
  / '+' _? d:name value_sep v:preco { return {value: v, desc: d} }
  / '-' _? v:preco { return {value: -v} }
  / '-' _? d:name value_sep v:preco { return {value: -v, desc: d} }
total
  = newline _? '=' _? v:preco { return v }
  / newline _? 'total'i _? colon _? v:preco { return v }

venda
  = i:item _ 'no'i _ p:pag { i.pagamento = p; return i }
  / i:item _ 'em'i _ p:pag { i.pagamento = p; return i }
  / i:item value_sep p:pag { i.pagamento = p; return i }
  / i:item _ 'de'i _ x:x { i.pagamento = 'crédito'; i.x = x; return i } 
  / i:item _ 'em'i _ x:x { i.pagamento = 'crédito'; i.x = x; return i } 
  / i:item value_sep x:x { i.pagamento = 'crédito'; i.x = x; return i }
  / i:item value_sep p:pag value_sep x:x { i.x = x; i.p = p == 'cartão' ? 'crédito' : p; return i }
  / i:item value_sep x:x value_sep p:pag { i.x = x; i.p = p == 'cartão' ? 'crédito' : p; return i }
  / i:item _ 'em'i _ x:x _ 'no'i _ p:pag { i.x = x; i.p = p == 'cartão' ? 'crédito' : p; return i }
  / i:item _ 'de'i _ x:x _ 'no'i _ p:pag { i.x = x; i.p = p == 'cartão' ? 'crédito' : p; return i }
  / i:item { i.pagamento = 'dinheiro'; return i }
pag
  = 'crédito'i {return 'crédito'}
  / 'credito'i {return 'crédito'}
  / 'créd'i {return 'crédito'}
  / 'cred'i {return 'crédito'}
  / 'cartão'i _ 'de'i _ 'crédito'i {return 'crédito'}
  / 'cartao'i _ 'de'i _ 'crédito'i {return 'crédito'}
  / 'cartão'i _ 'de'i _ 'credito'i {return 'crédito'}
  / 'cartao'i _ 'de'i _ 'credito'i {return 'crédito'}
  / 'débito'i {return 'débito'}
  / 'debito'i {return 'débito'}
  / 'déb'i {return 'débito'}
  / 'deb'i {return 'débito'}
  / 'cartão'i _ 'de'i _ 'débito'i {return 'débito'}
  / 'cartao'i _ 'de'i _ 'débito'i {return 'débito'}
  / 'cartão'i _ 'de'i _ 'debito'i {return 'débito'}
  / 'cartao'i _ 'de'i _ 'debito'i {return 'débito'}
  / 'dinheiro'i {return 'dinheiro'}
  / 'cheque'i {return 'cheque'}
  / 'cartão'i {return 'cartão'}
  / 'cartao'i {return 'cartão'}
  / 'cart'i {return 'cartão'}
x
  = n:num _ 'vezes' { return parseInt(n) }
  / n:num _? 'x' { return parseInt(n) }

item = _* item:_item { return item }
_item
  = i:quant quant_sep? n:name value_sep v:preco {
      var item = i
      item.item = n
      item.value = v
      return item
    }
  / v:preco value_sep? i:quant quant_sep n:name {
      var item = i
      item.item = n
      item.value = v
      return item
    }
  / v:preco value_sep? n:name quant_sep i:quant {
      var item = i
      item.item = n
      item.value = v
      return item
    }
  / i:quant quant_sep n:name value_sep v:preco {
      var item = i
      item.item = n
      item.value = v
      return item
    }
  / v:preco value_sep i:quant n:name {
      var item = i
      item.item = n
      item.value = v
      return item
    }
  / v:preco value_sep u:unit quant_sep n:name {
      return {
        item: n,
        u: u,
        q: 1,
        value: v
      }
    }
  / v:preco value_sep n:name {
      return {
        item: n,
        u: '-',
        q: '?',
        value: v
      }
    }
  / v:preco value_sep n:name i:quant {
      var item = i
      item.item = n
      item.value = v
      return item
    }
  / u:unit quant_sep n:name value_sep v:preco {
    return {
      item: n,
      u: u,
      q: 1,
      value: v
    }
  }
  / n:name value_sep v:preco {
    return {
      item: n,
      u: '-',
      q: '?',
      value: v
    }
  }
  / n:name quant_sep i:quant value_sep v:preco {
      var item = i
      item.item = n
      item.value = v
      return item
  }


preco
  = reais _? v:value { return parseInt(v*100) }
  / v:value _ reais { return parseInt(v*100) }
  / v:value _ centavos { return parseInt(v) }
  / v:value { return parseInt(v*100) }

quant
  = q:value _? u:unit _? {
    if (u == 'g') {
      u = 'kg'
      q = q/1000
    }
    return {u: u, q: q}
  }
  / q:value {
    return  {
      u: 'u',
      q: q
    }
  }
  
quant_sep
  = _? 'de'i _?
  / _? semicolon _?
  / _? comma _?
  
value_sep
  = _? colon _?
  
unit
  = p:package _ 'de'i _ c:value _? m:measure { return p + '/' + c + m }
  / p:package _ c:value _? m:measure { return p + '/' + c + m }
  / p:package { return p }
  / m:measure { return m }

package
  = 'u'i 'nidade'? 's'i? { return 'unidade' }
  / 'garrafa'i 's'i? { return 'garrafa' }
  / 'penca'i 's'i? { return 'penca' }
  / 'bandeja'i 's'i? { return 'bandeja' }
  / 'bandejinha'i 's'i? { return 'bandeja' }
  / 'vidro'i 's'i? { return 'vidro' }
  / 'vdr'i 's'i? { return 'vidro' }
  / 'vd'i 's'i? { return 'vidro' }
  / 'lata'i 's'i? { return 'lata' }
  / 'lt'i 's'i? { return 'lata' }
  / 'pote'i 's'i? { return 'pote' }
  / 'pt'i 's'i? { return 'pote' }
  / 'potinho'i 's'i? { return 'pote' }
  / 'tanto'i 's'i? { return 'punhado' }
  / 'punhado'i { return 'punhado' }
  / 'ramo'i 's'i? { return 'ramo' }
  / 'pct'i 's'i? { return 'pacote' }
  / 'pacote'i 's'i? { return 'pacote' }
  / 'saco'i 's'i? { return 'saco' }
  / 'saquinho'i 's'i? { return 'saco' }
  / 'sc'i 's'i? { return 'saco' }
  / 'cx'i 's'i? { return 'caixa' }
  / 'caixa'i 's'i? { return 'caixa' }
  / 'd' [úu] 'zia'i 's'i? { return 'dúzia' }
  / 'dz'i 's'i? { return 'dúzia' }

measure
  = 'kg'i 's'i? { return 'kg' }
  / 'quilo'i 's'i? { return 'kg' }
  / 'kilo'i 's'i? { return 'kg' }
  / 'grama'i 's'i? { return 'g' }
  / 'g'i { return 'g' }
  / 'ml'i 's'i? { return 'ml' }
  / 'mg'i 's'i? { return 'mg' }
  / 'litro'i 's'i? { return 'litro' }
  / 'l'i { return 'litro' }

reais = 'reais'i / 'real'i / 'R$'i / 'BRL'
centavos = 'centavo'i 's'i? / 'cent'i 's'i?

name
  = l1:letter+ '-' l2:letter+ '-' l3:letter+ { return (l1.join('') + '-' + l2.join('') + '-' + l3.join('')).toLowerCase() }
  / l1:letter+ '-' l2:letter+ { return (l1.join('') + '-' + l2.join('')).toLowerCase() }
  / l:letter+ { return l.join('').trim().toLowerCase() }
letter = [A-Za-z\u0080-\u00FF0-9 ]i
value = n:num+ { return parseFloat(n.join('').replace(',', '.')) }
num = [,0-9]
space = " "
blank = [\n ]
_ = space+
colon = ":"
comma = ","
semicolon = ';'
newline = "\n"
any = [^\n]

